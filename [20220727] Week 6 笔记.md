# Week 6

## Lecture 1

这节课开始讲述 scheme。

Scheme 程序由 expression 组成。expression 可以是 primitive expressions，可以是 combinations。任何不是 call expression 的 combination 都是 special forms。下面是几个例子：

- If expression:  `(if <predicate> <consequent> <alternative>)`
- And and or: `(and <e1> ... <e2>)`, `(or <e1> ... <e2>)`
- Binding symbols: `(define <symbol> <expression>)`
- New procedures: `(define (<symbol> <formal> <parameters>) <body>)`
- Lambda procedures: `(lambda (<formal-parameters>) <body>)`

视频中对这些特殊形式都给出了例子。

然后讲述了 lisp 中的 list。所有 Lisp 中的 list 都是 linked list。有 `cons`，`car`，`card`，`nil` 这些基本关键字来帮助操作 list，也可以用 `list` 过程来创建 list。这些抽象在 SICP 书中已经看到过了。视频在 [CS61A 的 Scheme 解释器](https://code.cs61a.org/scheme/) 中演示了例子。

接着就进入了 lisp 的 symbolic programming，介绍重要的概念 quotation。quotaion 是用来指代符号的。`'<e>` 是 `(quote <e>)` 的简写，后者是一个 special form。quotaion 也可以用在 list，但是注意 `'(1 a)` 和 `(list 1 a)` 并不相同。然后也介绍了unquote 符号 `,<e>` 和 quasiquote 符号 `` `<e> ``，这两者对应等价于 `(unquote <e>)` 和 `(quasiquote <e>)` 这两个 special form。

```scheme
(define (cadr lst) (car (cdr lst)))
(define (caddr lst) (car (cdr (cdr lst))))
; (1 + 1)       ==> (+ 1 1)
; ((2 * 3) + 4) ==> (+ (* 2 3) 4)
; assume e is a quotation of an infix expression or a number
(define (infix e)
  (if (not (list? e)) e
    `(,(cadr e) ,(infix (car e)) ,(infix (caddr e)))
  )
)
```

教授展示了用 scheme 画史宾斯基三角形的例子。在这个例子中加载文件是用过程（函数） `(load 'ex.scm)` 来做的。

```scheme
(define (repeat k fn)
  (fn)
  (if (> k 1) (repeat (- k 1) fn)))

(define (tri fn)
  (repeat 3 (lambda () (fn) (lt 120))))

(define (sier d k)
  (tri (lambda () (if (= d 1) (fd k) (leg d k)))))

(define (leg d k)
  (sier (- d 1) (/ k 2))
  (penup) (fd (/ k 3)) (pendown))
```

最后在普遍意义上讲述了一下 programming language。High-level language 提供了各种各样的抽象。最强大的抽象就是构造新的 programming language。一个 programming language 有 syntax 和 semantics。创建一个新的 programming language 需要创建一个 specification，或者创建一个 canonical implementation。Scheme 先有了前者，而 Python 先有了后者。现在大型的语言是两者都有的。

万圣节，吟诗一首。

## Lecture 2

这节课讲述 interpreter。

首先介绍了 parsing，从 text 生成 tokens。parsing 是一个迭代过程，检查 tokens 是否合法，判断 tokens 的类型，并且一次处理一行。然后介绍了 syntactic analysis，这个过程接受 tokens，生成 expression。Syntactic analysis 是一个递归过程。

课程举了一个解释器的例子：scheme calculator。解释了这个例子当中的类和函数，也介绍了 Read-Eval-Print Loop。一个有意思的问题是什么时候 raise 什么类型的 error，介绍了 ValueError，SyntaxError 和 TypeError，运算过程中可能出现的 ZeroDivisionError，和用来对程序进行控制的 EOFError 和 KeyboardInterrupt。

然后，课程讲述了 scheme 解释器的结构。Eval 和 Apply 是 mutual recursion。Eval 过程需要一个 environment 用来 symbol lookup，而 Apply 创建一个新的 environment。教授用 trace 分析了 Eval 过程。Special forms 是在 Eval 当中处理的，这节课也介绍了 special forms 的实现，包括 logical forms，quotation，lambda 和 define。

## Lecture 3

这节课讲述更多 scheme 相关的话题。

首先是 dynamic scope 和 lexical scope。Dynamic scope 是 procedure 被 called 的 frame，而 lexical scope 是 procedure 被 defined 的 frame。在 python 和 scheme 中的 name lookup 都是看 lexical scope。而实际上 dynamic scope 在另一些语言中也有被用到。

然后讲述 tail recursion。

在引入时介绍了 FP 及其优势。FP 一般所有函数都是 prue function；没有 re-assignment 和 mutable data types；并且 name-value bindings 是永久的。FP 的优势有：表达式的值和子表达式的求值顺序无关；子表达式可以被并行，或者只在被需要的时候进行求值（lazily）；以及引用透明：子表达式改变了，但是值不变，那么就不会产生任何影响。但是 FP 没有 for 和 while，全是函数调用，在效率上会有问题。

因此引入了 tail recursion。在 Python 当中，递归调用总是会创建新的 frames。视频比较了 factorial 函数的递归和迭代方法的时间和空间复杂度。Scheme 的实现需要是 properly tail-recursive 的，这允许迭代计算仅消耗常数空间，即使这个迭代计算是在语法上用递归过程描述的。

一些 call 是 tail calls，而一些不是，关键的区别是在 call 结束的时候是否还有更多工作要做。tail call 是在 tail context 当中的 call expression。而 tail context 包括：lambda 表达式的最后一个子表达式；在 tail context 的 if 表达式中的第二个和第三个子表达式；所有在 tail context 当中的 cond 表达式中的 non-predicate 子表达式；在 tail context 中的 and 或者 or 表达式中的最后一个表达式；在 tail context 中的 begin 表达式中的最后一个表达式。课程举了计算 list 长度的两种递归方法，一种是尾递归，另一种不是。

```scheme
(define (length s)
  (if (null? s) 0
    (+ 1 (length (cdr s)))))

(define (length-tail s)
  (define (length-iter s n)
    (if (null? s) n
      (length-iter (cdr s) (+ 1 n))))
  (length-iter s 0))
```

然后介绍了两个 HOF：reduce 和 map。Reduce 是否只需要 constant space 的取决于 reduce 的那个 procedure。Map 是否是 tail recursive 要根据实现，正确的实现是否只需要 constant space 也取决于 procedure。

```scheme
; not tail recursive
(define (map procedure s)
  (if (null? s)
    nil
    (cons (procedure (car s))
          (map procedure (cdr s)))))

; tail recursive
(define (map procedure s)
  (define (map-reverse s m)
    (if (null? s)
      m
      (map-reverse (cdr s)
                   (cons (procedure (car s))
                          m))))
  (reverse (map-reverse s nil)))

(define (reverse s)
  (define (reverse-iter s r)
    (if (null? s)
        r
        (reverse-iter (cdr s)
                      (cons (car s) r))))
  (reverse-iter s nil))
```

## Lecture 4

这节课首先把 program 当成 data，介绍了 eval 过程。在 scheme 和 python 中，很容易写一个可以写程序的程序。

然后介绍了 macros，这种机制可以让我们自己定义 special forms。这意味着可以改变 language 本身。实现的原理是在 scheme 中可以很容易把 program 当成 data。Macros 是在程序的 source code 的求值之前对其进行的操作。Scheme 有一个 special form `define-macro` 来定义一个 source code transformation。下面是一个例子（`begin` 是一个 special form，作用是对每个后续子表达式求值，整个表达式的结果是最后一个子表达式的值）:

```scheme
(define-macro (twice expr)
  (list 'begin expr expr))
```

```
> (twice (print 2))
2
2
```

另一个例子：

```scheme
(define-macro (check expr)
              (list 'if expr ''passed
                    (list 'quote (list 'failed: expr))))
; simplify
(define-macro (check expr) `(if ,expr 'passed '(failed: ,expr)))
; more clearly
(define-macro (check expr) `(if ,expr 'passed (quote (failed: ,expr))))
```

以及用 macro 创建 for：

```scheme
(define-macro (for sym vals expr)
  (list 'map (list 'lambda (list sym) expr) vals))

(define-macro (for sym vals expr)
  `(map (lambda (,sym) ,expr) ,vals))

(for x '(1 2 3) (* x (- x 1)))
```